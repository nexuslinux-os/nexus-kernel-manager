# Maintainer: Nexus Linux
# Fork of cachyos-kernel-manager (GPL-3.0-or-later). Qt kernel manager for
# Nexus Linux; source tracks the upstream CachyOS repo (signed tag) plus the
# alpm installed-db patch.

pkgname=nexus-kernel-manager
pkgver=1.19.0
pkgrel=1
pkgdesc="Simple kernel manager"
arch=(aarch64 x86_64)
url="https://github.com/nexuslinux/nexuslinux"
license=(GPL-3.0-or-later)
depends=('qt6-base' 'polkit' 'chwd' 'scx-manager')
makedepends=('cmake' 'ninja' 'git' 'python' 'polkit-qt6' 'qt6-tools' 'cargo' 'lld' 'clang' 'llvm')
conflicts=('cachyos-kernel-manager')
provides=('cachyos-kernel-manager')
source=("kernel-manager::git+https://github.com/cachyos/kernel-manager.git#tag=v${pkgver}?signed"
        fix-installed-db.patch)
sha256sums=('1a7e6ac9e98c1b54e62d47e3ce4f6b0b2933af640329ad980c9b4f3399de1e2d'
            '455c136513110b88e8234b8e4b25fd739804122ba6a492cde369b1f76c90960c')
validpgpkeys=(
  'B1B70BB1CD56047DEF31DE2EB62C3D10C54D5DA9'  # Vladislav Nepogodin <nepogodin.vlad@gmail.com>
)

prepare() {
  cd ${srcdir}/kernel-manager

  # patch cmake to not check for existence of installeddb api
  patch -Np1 -i ../fix-installed-db.patch
}

build() {
  cd ${srcdir}/kernel-manager

  # we are building with llvm stack here, due to rust
  export AR=llvm-ar
  export CC=clang
  export CXX=clang++
  export NM=llvm-nm
  export RANLIB=llvm-ranlib

  export RUSTFLAGS="-Cembed-bitcode -C opt-level=3 -Ccodegen-units=1 -Clinker=clang -C link-arg=-flto -Clink-arg=-fuse-ld=/usr/bin/ld.lld"

  # Use a pre-populated CPM source cache so the fmt/frozen/corrosion deps are
  # not re-fetched from GitHub (previous builds stalled on that network fetch).
  export CPM_SOURCE_CACHE="${startdir}/cpm-cache"

  cmake -S . -Bbuild \
        -GNinja \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib
  cmake --build build
}

package() {
  cd ${srcdir}/kernel-manager

  # we are building with llvm stack here, due to rust
  export AR=llvm-ar
  export CC=clang
  export CXX=clang++
  export NM=llvm-nm
  export RANLIB=llvm-ranlib

  export RUSTFLAGS="-Cembed-bitcode -C opt-level=3 -Ccodegen-units=1 -Clinker=clang -C link-arg=-flto -Clink-arg=-fuse-ld=/usr/bin/ld.lld"

  export CPM_SOURCE_CACHE="${startdir}/cpm-cache"

  DESTDIR="${pkgdir}" cmake --build build --target install
}
